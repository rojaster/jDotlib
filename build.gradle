group 'org.astec.jdotlib'
version '1.0-SNAPSHOT'

buildscript{
    repositories{
        mavenCentral()
        jcenter()
    }
}

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

description = "I'm only the noise on the wires"

sourceCompatibility = 1.8
mainClassName='org.astec.jdotlib.dotgraph.main.JDotlibMain'
applicationDefaultJvmArgs = ['-Dlogback.configurationFile=src/main/resources/logback.groovy']

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

sourceSets{
    main{
        java{
            srcDir 'src/main/java'
        }
        resources{
            srcDir 'src/main/resources'
        }
    }
    test{
        java{
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

/**************************** GradleWrapper ***************************************************************************/
task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
/**************************** GradleWrapper ***************************************************************************/


/**************************** Source and Javadoc Artifacts ************************************************************/
task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts{
    archives sourcesJar
    archives javadocJar
    archives jar
}

uploadArchives.dependsOn  sourcesJar, javadocJar, jar
/**************************** Source and Javadoc Artifacts ************************************************************/


/**************************** Maven Upload Archives *******************************************************************/
configurations{
    oneJar
}

uploadArchives{
    repositories.mavenDeployer{
        configuration = configurations.oneJar

        repository(url: project.hasProperty("mavenLocalBase") ? "$mavenLocalBase/content/repositories/thirdparty" : ""){
            authentication(userName: project.hasProperty("mavenUsername") ? "$mavenUsername" : "",
                            password: project.hasProperty("mavenPassword") ? "$mavenPassword" : "")
        }

        pom.project{
            groupId project.group
            artifactId project.name
            version project.version.split('-')[0]

            name 'jdotlib'
            packaging 'jar'
            description 'jDotlib is a simple wrapper for DOT format. Has support official dot guide. #crazylazyrockhack'

            url 'https://github.com/rojaster/jDotlib'
            inceptionYear '2015'
            scm{
                url 'scm:git@github.com:rojaster/jDotlib.git'
                connection 'scm:git@github.com:rojaster/jDotlib.git'
                developerConnection 'scm:git@github.com:rojaster/jDotlib.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                    comments """
                                Copyright (c) 2015.
                                By Alekum, ASTEC Corporation. All Rights Reserved under MIT License.

                                Permission is hereby granted, free of charge, to any person obtaining a copy
                                of this software and associated documentation files (the "Software"), to deal
                                in the Software without restriction, including without limitation the rights
                                to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
                                copies of the Software, and to permit persons to whom the Software is
                                furnished to do so, subject to the following conditions:

                                THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
                                IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
                                FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
                                AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
                                LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
                                OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
                                THE SOFTWARE.
                             """
                    distribution 'repo'
                }
            }

            developers{
                developer{
                    id 'alekum'
                    name 'Alexey Kuzmenko'
                    email 'a.kuzmenko@dsec.ru'
                    roles{
                        role 'Research Engineer'
                    }
                }
            }
        }
    }
}
/**************************** Maven Upload Archives *******************************************************************/
repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.slf4j',           name: 'slf4j-api',       version: '1.7.+'
    compile group: 'ch.qos.logback',      name: 'logback-classic', version: '1.+'
    compile group: 'ch.qos.logback',      name: 'logback-core',    version: '1.+'
    compile group: 'org.codehaus.groovy', name: 'groovy-all',      version: '2.+'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.google.truth', name: 'truth', version: '0.27'
}
